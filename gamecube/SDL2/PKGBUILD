# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=gamecube-sdl2
pkgver=2.28.5
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('wii-pkg-config' 'dkp-toolchain-vars')
depends=('libogc')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('wii-portlibs' 'wii-sdl2')

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/cubevars.sh

  patch -Np1 -i $srcdir/SDL2-${pkgver}.patch

  powerpc-eabi-cmake -S. -B _build -DCMAKE_BUILD_TYPE=Release -GNinja

  cmake --build _build --parallel
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/cubevars.sh

  DESTDIR=${pkgdir} cmake --install _build

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
sha256sums=('332cb37d0be20cb9541739c61f79bae5a477427d79ae85e352089afdaf6666e4'
            'a74153065448746a60db169d20fea1a9e66e4a0e383e772825f74894cddfc3cb')
